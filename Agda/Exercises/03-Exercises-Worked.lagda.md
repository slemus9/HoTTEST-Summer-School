# Week 03 - Agda Exercises

## Please read before starting the exercises

**The exercises are designed to increase in difficulty so that we can cater to
our large and diverse audience. This also means that it is *perfectly fine* if
you don't manage to do all exercises: some of them are definitely a bit hard for
beginners and there are likely too many exercises! You *may* wish to come back
to them later when you have learned more.**

Having said that, here we go!

This is a markdown file with Agda code, which means that it displays nicely on
GitHub, but at the same time you can load this file in Agda and fill the holes
to solve exercises.

**Please make a copy of this file to work in, so that it doesn't get overwritten
  (in case we update the exercises through `git`)!**

```agda
{-# OPTIONS --without-K --allow-unsolved-metas #-}

module 03-Exercises-Worked where

open import prelude hiding (_âˆ¼_)
```

## Part I -- Homotopies

It is often convenient to work with *pointwise equality* of functions, defined as follows.
```agda
module _ {A : Type} {B : A â†’ Type} where
  _âˆ¼_ : ((x : A) â†’ B x) â†’ ((x : A) â†’ B x) â†’ Type
  f âˆ¼ g = âˆ€ x â†’ f x â‰¡ g x
```
An element of `f âˆ¼ g` is usually called a homotopy.

### Exercise 1 (â‹†â‹†)

Unsurprisingly, many properties of this type of pointwise equalities
can be inferred directly from the same operations on paths.

Try to prove reflexivity, symmetry and transitivity of `_âˆ¼_` by filling these holes.
```agda
  âˆ¼-refl : (f : (x : A) â†’ B x) â†’ f âˆ¼ f
  âˆ¼-refl f x = refl (f x)

  âˆ¼-inv : (f g : (x : A) â†’ B x) â†’ (f âˆ¼ g) â†’ (g âˆ¼ f)
  âˆ¼-inv f g H x = (H x) â»Â¹

  âˆ¼-concat : (f g h : (x : A) â†’ B x) â†’ f âˆ¼ g â†’ g âˆ¼ h â†’ f âˆ¼ h
  âˆ¼-concat f g h H K x = (H x) âˆ™ (K x)

  infix 0 _âˆ¼_
```

## Part II -- Isomorphisms

A function `f : A â†’ B` is called a *bijection* if there is a function `g : B â†’ A` in the opposite direction such that `g âˆ˜ f âˆ¼ id` and `f âˆ˜ g âˆ¼ id`. Recall that `_âˆ¼_` is [pointwise equality](identity-type.lagda.md) and that `id` is the [identity function](products.lagda.md). This means that we can convert back and forth between the types `A` and `B` landing at the same element we started with, either from `A` or from `B`. In this case, we say that the types `A` and `B` are *isomorphic*, and we write `A â‰… B`. Bijections are also called type *isomorphisms*. We can define these concepts in Agda using [sum types](sums.lagda.md) or [records](https://agda.readthedocs.io/en/latest/language/record-types.html). We will adopt the latter, but we include both definitions for the sake of illustration.
Recall that we [defined](general-notation.lagda.md) the domain of a function `f : A â†’ B` to be `A` and its codomain to be `B`.

We adopt this definition of isomorphisms using records.
```agda
record is-bijection {A B : Type} (f : A â†’ B) : Type where
 constructor
  Inverse
 field
  inverse : B â†’ A
  Î·       : inverse âˆ˜ f âˆ¼ id
  Îµ       : f âˆ˜ inverse âˆ¼ id

record _â‰…_ (A B : Type) : Type where
 constructor
  Isomorphism
 field
  bijection   : A â†’ B
  bijectivity : is-bijection bijection

open is-bijection public
open _â‰…_ public

infix 0 _â‰…_
```
*At one point, I thought I needed the following for something:*
```agda
â‰…-refl : (A : Type) â†’ A â‰… A
â‰…-refl A = record { bijection = id ; bijectivity = record { inverse = id ; Î· = âˆ¼-refl id ; Îµ = âˆ¼-refl id } }
```

### Exercise 2 (â‹†)

Reformulate the same definition using Sigma-types.
```agda
is-bijection' : {A B : Type} â†’ (A â†’ B) â†’ Type
is-bijection' {A} {B} f = Î£ inverse ê‰ (B â†’ A) , ((inverse âˆ˜ f âˆ¼ id) Ã— (f âˆ˜ inverse âˆ¼ id))

_â‰…'_ : Type â†’ Type â†’ Type
A â‰…' B = Î£ bijection ê‰ (A â†’ B) , (is-bijection' bijection)
```
The definition with `Î£` is probably more intuitive, but, as discussed above,
the definition with a record is often easier to work with,
because we can easily extract the components of the definitions using the names of the fields.
It also often allows Agda to infer more types, and to give us more sensible goals in the
interactive development of Agda programs and proofs.

Notice that `inverse` plays the role of `g`.
The reason we use `inverse` is that then we can use the word
`inverse` to extract the inverse of a bijection.
Similarly we use `bijection` for `f`, as we can use the word
`bijection` to extract the bijection from a record.

This type can be defined to be `ğŸ™ âˆ” ğŸ™` using the coproduct,
but we give a direct definition which will allow us to discuss some relationships between the various type formers of Basic MLTT.

```agda
data ğŸš : Type where
 ğŸ ğŸ : ğŸš
```

### Exercise 3 (â‹†â‹†)

Prove that ğŸš and Bool are isomorphic

```agda
Bool-ğŸš-isomorphism : Bool â‰… ğŸš
Bool-ğŸš-isomorphism = record { bijection = f ; bijectivity = f-is-bijection }
 where
  f : Bool â†’ ğŸš
  f false = ğŸ
  f true  = ğŸ

  g : ğŸš â†’ Bool
  g ğŸ = false
  g ğŸ = true

  gf : g âˆ˜ f âˆ¼ id
  gf true  = refl true
  gf false = refl false

  fg : f âˆ˜ g âˆ¼ id
  fg ğŸ = refl ğŸ
  fg ğŸ = refl ğŸ

  f-is-bijection : is-bijection f
  f-is-bijection = record { inverse = g ; Î· = gf ; Îµ = fg }
```


## Part III - Finite Types

In the last HoTT Exercises we encountered two definitions of the finite types.
Here we prove that they are isomorphic.
Note that `zero` was called `pt` and suc `i` on the HoTT exercise sheet.

```agda
data Fin : â„• â†’ Type where
 zero : {n : â„•} â†’ Fin (suc n)
 suc  : {n : â„•} â†’ Fin n â†’ Fin (suc n)
```

### Exercise 4 (â‹†)

Prove the elimination principle of `Fin`.
```agda
Fin-elim : (A : {n : â„•} â†’ Fin n â†’ Type)
         â†’ ({n : â„•} â†’ A {suc n} zero)
         â†’ ({n : â„•} (k : Fin n) â†’ A k â†’ A (suc k))
         â†’ {n : â„•} (k : Fin n) â†’ A k
Fin-elim A a f = h
 where
  h : {n : â„•} (k : Fin n) â†’ A k
  h zero    = a
  h (suc k) = f k (h k)
```

We give the other definition of the finite types and introduce some notation.

```agda
Fin' : â„• â†’ Type
Fin' 0       = ğŸ˜
Fin' (suc n) = ğŸ™ âˆ” Fin' n

zero' : {n : â„•} â†’ Fin' (suc n)
zero' = inl â‹†

suc'  : {n : â„•} â†’ Fin' n â†’ Fin' (suc n)
suc' = inr
```

### Exercise 5 (â‹†â‹†â‹†)

Prove that `Fin n` and `Fin' n` are isomorphic for every `n`.

```agda
Fin-isomorphism : (n : â„•) â†’ Fin n â‰… Fin' n
Fin-isomorphism n = record { bijection = f n ; bijectivity = f-is-bijection n }
 where
  f : (n : â„•) â†’ Fin n â†’ Fin' n
  f (suc n) zero    = zero'
  f (suc n) (suc k) = suc' (f n k)

  g : (n : â„•) â†’ Fin' n â†’ Fin n
  g (suc n) (inl â‹†) = zero
  g (suc n) (inr k) = suc (g n k)

  gf : (n : â„•) â†’ g n âˆ˜ f n âˆ¼ id
  gf (suc n) zero    = refl zero
  gf (suc n) (suc k) = Î³
   where
    IH : g n (f n k) â‰¡ k
    IH = gf n k

    Î³ = g (suc n) (f (suc n) (suc k)) â‰¡âŸ¨ ap (g (suc n)) (refl _) âŸ©
        g (suc n) (suc' (f n k))      â‰¡âŸ¨ refl _ âŸ©
        suc (g n (f n k))             â‰¡âŸ¨ ap suc IH âŸ©
        suc k                         âˆ

  fg : (n : â„•) â†’ f n âˆ˜ g n âˆ¼ id
  fg (suc n) (inl â‹†) = refl zero'
  fg (suc n) (inr k) = Î³
   where
    IH : f n (g n k) â‰¡ k
    IH = fg n k

    Î³ = f (suc n) (g (suc n) (suc' k)) â‰¡âŸ¨ ap (f (suc n)) (refl _) âŸ©
        f (suc n) (suc (g n k))        â‰¡âŸ¨ refl _ âŸ©
        suc' (f n (g n k))             â‰¡âŸ¨ ap suc' IH âŸ©
        suc' k                         âˆ

  f-is-bijection : (n : â„•) â†’ is-bijection (f n)
  f-is-bijection n = record { inverse = g n ; Î· = gf n ; Îµ = fg n}
```

## Part IV -- minimal elements in the natural numbers

In this section we establish some definitions which are needed to state and prove the well-ordering
principle of the natural numbers.

### Exercise 6 (â‹†)

Give the recursive definition of the less than or equals relation on the natural numbers.

```agda
_â‰¤â‚_ : â„• â†’ â„• â†’ Type
0     â‰¤â‚ y     = ğŸ™
suc x â‰¤â‚ 0     = ğŸ˜
suc x â‰¤â‚ suc y = x â‰¤â‚ y
```

### Exercise 7 (â‹†)

Given a type family `P` over the naturals, a lower bound `n` is a natural number such that
for all other naturals `m`, we have that if `P(m)` holds, then`n â‰¤â‚ m`.
Translate this definition into HoTT.

```agda
is-lower-bound : (P : â„• â†’ Type) (n : â„•) â†’ Type
is-lower-bound P n = âˆ€ m â†’ (P m) â†’ n â‰¤â‚ m
```

We define the type of minimal elements of a type family over the naturals.
```agda
minimal-element : (P : â„• â†’ Type) â†’ Type
minimal-element P = Î£ n ê‰ â„• , (P n) Ã— (is-lower-bound P n)
```

### Exercise 8 (â‹†)

Prove that all numbers are at least as large as zero.
```agda
leq-zero : (n : â„•) â†’ 0 â‰¤â‚ n
leq-zero n = â‹†
```


## Part V -- The well-ordering principle of â„•

Classically, the well-ordering principle states that every non-empty set
of natural numbers has a least element.

In HoTT, such subsets can be translated as decidable type family.
Recall the definition of decidability:
```agda
open import decidability
  using (is-decidable; is-decidable-predicate)
```

The well-ordering principle reads
```agda
Well-ordering-principle = (P : â„• â†’ Type) â†’ (d : is-decidable-predicate P) â†’ (n : â„•) â†’ P n â†’ minimal-element P
```

We shall prove this statement via induction on `n`.
In order to make the proof more readable, we first prove two lemmas.

### Exercise 9 (ğŸŒ¶)

What is the statement of `is-minimal-element-suc`
under the Curry-Howard interpretation?

Answer: it says that given a subset of $\mathbb{N}$ (i.e. a decidable type family `P`), then for all natural numbers $m$, if $m + 1$ is in the subset and if $m$ is a lower bound for the shifted-down type familyâ€”and if $0$ is not in the subsetâ€”then $m + 1$ is a lower bound for P. In other words, if your successor is in the subset and you're a lower bound for the shifted-down-by-one subset, then your successor is a lower bound for the subset (and therefore implicitly a minimal element)--as long as we didn't throw out an element of the subset when shifting it down by one.

Prove this lemma.

We could do it like this...
```agda
is-minimal-element-suc' :
  (P : â„• â†’ Type) (d : is-decidable-predicate P)
  (m : â„•) (pm : P (suc m))
  (is-lower-bound-m : is-lower-bound (Î» x â†’ P (suc x)) m) â†’
  Â¬ (P 0) â†’ is-lower-bound P (suc m)
is-minimal-element-suc' P d m pm is-lower-bound-m neg-p0 zero p = neg-p0 p
is-minimal-element-suc' P d zero pm is-lower-bound-m neg-p0 (suc n) p = â‹†
is-minimal-element-suc' P d (suc m) pm is-lower-bound-m neg-p0 (suc n) p = is-lower-bound-m n p
```
But you get the sense that we're relying on the specific nature of `_â‰¤â‚_` despite doing something more general. After all, we seem to be doing a very fundamental operation: shifting and gluing. Can we reflect that in our proof somehow?

To start with: a special case of `â„•-elim` where we're just gluing data together into a function:
```agda
glue-â„• : (Q : â„• â†’ Type) â†’ (Q 0) â†’ ((n' : â„•) â†’ Q (suc n')) â†’ (n : â„•) â†’ Q n
glue-â„• Q q0 qsuc zero = q0
glue-â„• Q q0 qsuc (suc n) = qsuc n
```
Whenever a two-argument type family `H` over the natural numbers (which is meant to represent `_â‰¤â‚_`) satisfies a certain diagonal condition, we can glue certain data together.

This is what's going on "in general" with the `is-minimal-element-suc` proof.
```agda
glue-diagonal-suc :
  (H : â„• â†’ â„• â†’ Type)
  (diagH : âˆ€ m n â†’ (H m n) â†’ (H (suc m) (suc n)))
  (P : â„• â†’ Type)
  (m : â„•)
  (phzero : (P 0) â†’ (H (suc m) 0)) â†’
  (shift : (n : â„•) â†’ P (suc n) â†’ H m n) â†’
  ((n' : â„•) â†’ P n' â†’ H (suc m) n')
glue-diagonal-suc H diagH P m phzero shift =
 glue-â„• (Î» n â†’ P n â†’ H (suc m) n) phzero (Î» n' â†’ Î» psuc â†’ (diagH m n') (shift n' psuc))
```
The trick to applying this here is to recognize that `Â¬ (P 0)` is the same as `P 0 â†’ (suc m) â‰¤â‚ 0`, so
`neg-p0` furnishes our `0` case, and that `_â‰¤â‚_` judgmentally satisfies the diagonal condition, so that we can just use `id` for `diagH`.
```agda
is-minimal-element-suc :
  (P : â„• â†’ Type) (d : is-decidable-predicate P)
  (m : â„•) (pm : P (suc m))
  (is-lower-bound-m : is-lower-bound (Î» x â†’ P (suc x)) m) â†’
  Â¬ (P 0) â†’ is-lower-bound P (suc m)
is-minimal-element-suc P _ m _ is-lower-bound-m neg-p0 =
 glue-diagonal-suc _â‰¤â‚_ (Î» n m â†’ id) P m neg-p0 is-lower-bound-m
```

### Exercise 10 (ğŸŒ¶)

What is the statement of `well-ordering-principle-suc`
under the Curry-Howard interpretation?

Answer: Given a subset of $\mathbb{N}$, we can use the minimal element of the shifted-down version of that subset plus whether 0 is in the subset to figure out what the minimal element of the subset is.

Prove this lemma.

```agda
well-ordering-principle-suc :
  (P : â„• â†’ Type) (d : is-decidable-predicate P)
  (n : â„•) (p : P (suc n)) â†’
  is-decidable (P 0) â†’
  minimal-element (Î» m â†’ P (suc m)) â†’ minimal-element P
well-ordering-principle-suc P d n p (inl p0) _  = 0 , (p0 , (Î» _ _ â†’ â‹†))
well-ordering-principle-suc P d n p (inr neg-p0) (m , (pm , is-min-m)) =
  (suc m) , (pm , is-minimal-element-suc P d m pm is-min-m neg-p0)
```

### Exercise 11 (ğŸŒ¶)

Use the previous two lemmas to prove the well-ordering principle
```agda
well-ordering-principle : (P : â„• â†’ Type) â†’ (d : is-decidable-predicate P) â†’ (n : â„•) â†’ P n â†’ minimal-element P
well-ordering-principle P d 0 p = 0 , (p , (Î» _ _ â†’ â‹†))
well-ordering-principle P d (suc n) p =
  well-ordering-principle-suc P d n p (d 0) (well-ordering-principle (Î» x â†’ P (suc x)) (d âˆ˜ suc) n p)
```

### Exercise 12 (ğŸŒ¶)

Prove that the well-ordering principle returns 0 if `P 0` holds.

```agda
is-zero-well-ordering-principle-suc :
  (P : â„• â†’ Type) (d : is-decidable-predicate P)
  (n : â„•) (p : P (suc n)) (d0 : is-decidable (P 0)) â†’
  (x : minimal-element (Î» m â†’ P (suc m))) (p0 : P 0) â†’
  (prâ‚ (well-ordering-principle-suc P d n p d0 x)) â‰¡ 0
is-zero-well-ordering-principle-suc P d n p (inl p0) x q0 = refl _
is-zero-well-ordering-principle-suc P d n p (inr np0) x q0 = ğŸ˜-nondep-elim (np0 q0)

is-zero-well-ordering-principle :
  (P : â„• â†’ Type) (d : is-decidable-predicate P) â†’
  (n : â„•) â†’ (pn : P n) â†’
  P 0 â†’
  prâ‚ (well-ordering-principle P d n pn) â‰¡ 0
is-zero-well-ordering-principle P d zero p p0 = refl _
is-zero-well-ordering-principle P d (suc m) pm =
  is-zero-well-ordering-principle-suc P d m pm (d 0) (well-ordering-principle (Î» x â†’ P (suc x)) (d âˆ˜ suc) m pm)
```
  